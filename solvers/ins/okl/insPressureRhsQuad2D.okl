/*

The MIT License (MIT)

Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/

#define surfaceTerms(sk,face,i, j)                                      \
{                                                                       \
  const dfloat nx = sgeo[sk*p_Nsgeo+p_NXID];                            \
  const dfloat ny = sgeo[sk*p_Nsgeo+p_NYID];                            \
  const dfloat WsJ = sgeo[sk*p_Nsgeo+p_WSJID];                          \
  const dfloat hinv= sgeo[sk*p_Nsgeo+p_IHID];                           \
                                                                        \
  dfloat dpdxP=0.f, dpdyP=0.f, pP=0.f;                                  \
  const int bc = EToB[face+p_Nfaces*e];                                 \
  if(bc>0) {                                                            \
    const dlong idM = vmapM[sk];                                        \
    insPressureDirichletConditions2D(bc,nu,time, x[idM], y[idM], nx, ny, 0.f, &pP);\
    insPressureNeumannConditions2D(bc,nu,time, x[idM], y[idM], nx, ny, 0.f, 0.f, &dpdxP, &dpdyP);\
  }                                                                     \
                                                                        \
  const dfloat dp = pP;                                                 \
  s_dpdx[j][i] += WsJ*nx*dp;                                            \
  s_dpdy[j][i] += WsJ*ny*dp;                                            \
  s_rhsp[j][i] -= WsJ*(nx*dpdxP + ny*dpdyP+ tau*dp*hinv);               \
}

// compute RHS = MM*RHS/gamma + BCdata
@kernel void insPressureRhsQuad2D(const dlong Nelements,
                               @restrict const  dfloat *  vgeo,
                               @restrict const  dfloat *  sgeo,
                               @restrict const  dfloat *  D,
                               @restrict const  dfloat *  LIFTT,
                               @restrict const  dfloat *  MM,
                               @restrict const  dlong  *  vmapM,
                               @restrict const  int    *  EToB,
                               const dfloat tau,
                               const dfloat time,
                               @restrict const  dfloat *  x,
                               @restrict const  dfloat *  y,
                               @restrict const  dfloat *  z,
                               const dfloat nu,
                               const dfloat gamma,
                               @restrict        dfloat *  RHS){

  for(dlong e=0;e<Nelements;++e;@outer(0)){

    @shared dfloat s_dpdx[p_Nq][p_Nq];
    @shared dfloat s_dpdy[p_Nq][p_Nq];
    @shared dfloat s_rhsp[p_Nq][p_Nq];
    @shared dfloat s_D[p_Nq][p_Nq];

    // loop over slabs
    for(int j=0;j<p_Nq;++j){
      for(int i=0;i<p_Nq;++i;@inner(0)){
        s_dpdx[j][i] = 0.f;
        s_dpdy[j][i] = 0.f;
        s_rhsp[j][i] = 0.f;

        s_D[j][i] = D[j*p_Nq+i];
      }
    }

    @barrier("local");

    // loop over faces to add pseudo-gradient

    // face 0 & 2
    for(int i=0;i<p_Nq;++i;@inner(0)){
      const dlong sk0 = e*p_Nfp*p_Nfaces + 0*p_Nfp + i ;
      const dlong sk2 = e*p_Nfp*p_Nfaces + 2*p_Nfp + i ;

      surfaceTerms(sk0,0,i,0     );
      surfaceTerms(sk2,2,i,p_Nq-1);
    }

    @barrier("local");

    // face 1 & 3
    for(int j=0;j<p_Nq;++j;@inner(0)){
      const dlong sk1 = e*p_Nfp*p_Nfaces + 1*p_Nfp + j;
      const dlong sk3 = e*p_Nfp*p_Nfaces + 3*p_Nfp + j;

      surfaceTerms(sk1,1,p_Nq-1,j);
      surfaceTerms(sk3,3,0     ,j);
    }

    @barrier("local");

    // prescale by geofacs
    for(int j=0;j<p_Nq;++j){
      for(int i=0;i<p_Nq;++i;@inner(0)){
        // does need the nasty geofacs
        const dlong gid = i + j*p_Nq + e*p_Np*p_Nvgeo;
        const dfloat drdx = vgeo[gid + p_RXID*p_Np];
        const dfloat drdy = vgeo[gid + p_RYID*p_Np];
        const dfloat dsdx = vgeo[gid + p_SXID*p_Np];
        const dfloat dsdy = vgeo[gid + p_SYID*p_Np];

        // chain rule (need to scale by wei
        const dfloat dpdx = s_dpdx[j][i];
        const dfloat dpdy = s_dpdy[j][i];

        s_dpdx[j][i] = (drdx*dpdx + drdy*dpdy);
        s_dpdy[j][i] = (dsdx*dpdx + dsdy*dpdy);
      }
    }

    @barrier("local");

    // loop over slabs
    for(int j=0;j<p_Nq;++j){
      for(int i=0;i<p_Nq;++i;@inner(0)){
        // does need the nasty geofacs
        const dlong gid = i + j*p_Nq + e*p_Np*p_Nvgeo;
        const dfloat JW = vgeo[gid + p_JWID*p_Np];

        dfloat dpr = 0, dps = 0;

        for(int n=0;n<p_Nq;++n){
          dpr += s_D[n][i]*s_dpdx[j][n];
          dps += s_D[n][j]*s_dpdy[n][i];
        }

        //fetch rhs value and add boundary terms
        const dlong id = e*p_Np + j*p_Nq + i;
        RHS[id] = JW*RHS[id]/gamma - (s_rhsp[j][i]  + dpr + dps);
      }
    }
  }
}
#undef surfaceTerms

#define surfaceTerms(sk,face,i, j)                                      \
{                                                                       \
  const dlong idM = vmapM[sk];                                          \
  const int bc = mapB[idM];                                             \
  if(bc>0) {                                                            \
    const dfloat nx = sgeo[sk*p_Nsgeo+p_NXID];                          \
    const dfloat ny = sgeo[sk*p_Nsgeo+p_NYID];                          \
                                                                        \
    const dfloat pM = P[idM];                                           \
    dfloat pP=0;                                                        \
    insPressureDirichletConditions2D(bc, nu, time, x[idM], y[idM], nx, ny, pM, &pP);\
    P[idM] = pP;                                                        \
  }                                                                     \
}

// enter BCs if C0
@kernel void insPressureBCQuad2D(const dlong Nelements,
                               @restrict const  dfloat *  sgeo,
                               @restrict const  dlong  *  vmapM,
                               @restrict const  int    *  mapB,
                               const dfloat time,
                               @restrict const  dfloat *  x,
                               @restrict const  dfloat *  y,
                               @restrict const  dfloat *  z,
                               const dfloat nu,
                               @restrict        dfloat *  P){

  for(dlong e=0;e<Nelements;e++;@outer(0)){
    // face 0 & 2
    for(int i=0;i<p_Nq;++i;@inner(0)){
      const dlong sk0 = e*p_Nfp*p_Nfaces + 0*p_Nfp + i;
      const dlong sk2 = e*p_Nfp*p_Nfaces + 2*p_Nfp + i;

      surfaceTerms(sk0,0,i,0     );
      surfaceTerms(sk2,2,i,p_Nq-1);
    }

    @barrier("local");

    // face 1 & 3
    for(int j=0;j<p_Nq;++j;@inner(0)){
      const dlong sk1 = e*p_Nfp*p_Nfaces + 1*p_Nfp + j;
      const dlong sk3 = e*p_Nfp*p_Nfaces + 3*p_Nfp + j;

      surfaceTerms(sk1,1,p_Nq-1,j);
      surfaceTerms(sk3,3,0     ,j);
    }
  }
}
