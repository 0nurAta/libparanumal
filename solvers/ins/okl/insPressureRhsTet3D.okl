/*

The MIT License (MIT)

Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/

// compute RHS = MM*RHS/gamma + BCdata
@kernel void insPressureRhsTet3D(const dlong Nelements,
                               @restrict const  dfloat *  vgeo,
                               @restrict const  dfloat *  sgeo,
                               @restrict const  dfloat *  Dmatrices,
                               @restrict const  dfloat *  LIFTT,
                               @restrict const  dfloat *  MM,
                               @restrict const  dlong  *  vmapM,
                               @restrict const  int    *  EToB,
                               const dfloat tau,
                               const dfloat time,
                               @restrict const  dfloat *  x,
                               @restrict const  dfloat *  y,
                               @restrict const  dfloat *  z,
                               const dfloat nu,
                               const dfloat gamma,
                               @restrict       dfloat *  RHS){

  for(dlong e=0;e<Nelements;++e;@outer(0)){
    @shared  dfloat s_dpdx[p_Np];
    @shared  dfloat s_dpdy[p_Np];
    @shared  dfloat s_dpdz[p_Np];

    @shared  dfloat s_lapp[p_Np];

    @shared  dfloat s_nxdp[p_NfacesNfp];
    @shared  dfloat s_nydp[p_NfacesNfp];
    @shared  dfloat s_nzdp[p_NfacesNfp];

    @shared  dfloat s_lappflux[p_NfacesNfp];

    @shared  dfloat s_Lnxdp[p_Np];
    @shared  dfloat s_Lnydp[p_Np];
    @shared  dfloat s_Lnzdp[p_Np];

    @exclusive dlong idM;
    @exclusive dfloat nx, ny, nz, sJ, invJ, hinv;

    for(int n=0;n<p_maxNodes;++n;@inner(0)){
      if(n<p_NfacesNfp){
        const dlong id  = n + e*p_Nfaces*p_Nfp;
        idM = vmapM[id];

        const int face = n/p_Nfp;
        // load surface geofactors for this face
        const dlong sid = p_Nsgeo*(e*p_Nfaces+face);
        nx    = sgeo[sid+p_NXID];
        ny    = sgeo[sid+p_NYID];
        nz    = sgeo[sid+p_NZID];
        sJ    = sgeo[sid+p_SJID];
        invJ  = sgeo[sid+p_IJID];
        hinv  = sgeo[sid+p_IHID];

        dfloat dpdxP=0.f, dpdyP=0.f, dpdzP=0.f, pP=0;

        const int bc = EToB[face+p_Nfaces*e];
        if(bc>0) {
          insPressureDirichletConditions3D(bc,nu,time, x[idM], y[idM], z[idM], nx, ny, nz, 0.f, &pP);
          insPressureNeumannConditions3D(bc,nu,time, x[idM], y[idM], z[idM], nx, ny, nz, 0.f, 0.f,0.f, &dpdxP, &dpdyP, &dpdzP);
        }

        const dfloat dp = pP;
        s_nxdp[n] = sJ*invJ*nx*dp;
        s_nydp[n] = sJ*invJ*ny*dp;
        s_nzdp[n] = sJ*invJ*nz*dp;
        s_lappflux[n] = sJ*invJ*(-nx*(dpdxP)-ny*(dpdyP)-nz*(dpdzP)-tau*hinv*dp);
      }
    }

    @barrier("local");

    // dpdx += LIFT*(sJ/J)*nx*dp
    for(int n=0;n<p_maxNodes;++n;@inner(0)){
      if(n<p_Np){
        const dlong gid = e*p_Nvgeo;
        const dfloat drdx = vgeo[gid + p_RXID];
        const dfloat drdy = vgeo[gid + p_RYID];
        const dfloat drdz = vgeo[gid + p_RZID];

        const dfloat dsdx = vgeo[gid + p_SXID];
        const dfloat dsdy = vgeo[gid + p_SYID];
        const dfloat dsdz = vgeo[gid + p_SZID];

        const dfloat dtdx = vgeo[gid + p_TXID];
        const dfloat dtdy = vgeo[gid + p_TYID];
        const dfloat dtdz = vgeo[gid + p_TZID];

        dfloat Lnxdp = 0.f;
        dfloat Lnydp = 0.f;
        dfloat Lnzdp = 0.f;

        #pragma unroll p_NfacesNfp
          for(int i=0;i<p_NfacesNfp;++i){
            dfloat liftt = LIFTT[n+i*p_Np];
            Lnxdp += liftt*s_nxdp[i];
            Lnydp += liftt*s_nydp[i];
            Lnzdp += liftt*s_nzdp[i];
          }

        const dfloat dpdx = Lnxdp;
        const dfloat dpdy = Lnydp;
        const dfloat dpdz = Lnzdp;

        s_dpdx[n] = drdx*dpdx + drdy*dpdy + drdz*dpdz; // abuse of notation
        s_dpdy[n] = dsdx*dpdx + dsdy*dpdy + dsdz*dpdz;
        s_dpdz[n] = dtdx*dpdx + dtdy*dpdy + dtdz*dpdz;

        s_Lnxdp[n] = Lnxdp;
        s_Lnydp[n] = Lnydp;
        s_Lnzdp[n] = Lnzdp;
      }
    }

    @barrier("local");

    for(int n=0;n<p_maxNodes;++n;@inner(0)){
      if(n<p_NfacesNfp){
        const int id = idM%p_Np;
        s_lappflux[n] += sJ*invJ*(nx*s_Lnxdp[id]+ny*s_Lnydp[id]+nz*s_Lnzdp[id]);
      }

      if(n<p_Np){
        dfloat lasr = 0.f, lass = 0.f, last=0.f;
        #pragma unroll p_Np
        for(int i=0;i<p_Np;++i) {
          const dfloat Drn = Dmatrices[n+i*p_Np+0*p_Np*p_Np];
          const dfloat Dsn = Dmatrices[n+i*p_Np+1*p_Np*p_Np];
          const dfloat Dtn = Dmatrices[n+i*p_Np+2*p_Np*p_Np];
          lasr += Drn*s_dpdx[i];
          lass += Dsn*s_dpdy[i];
          last += Dtn*s_dpdz[i];
        }

        //fetch rhs value and add volume terms
        const dlong id = e*p_Np+n;
        s_lapp[n] = RHS[id]/gamma +(lasr+lass+last);
      }
    }

    @barrier("local");

    for(int n=0;n<p_maxNodes;++n;@inner(0)){
      if(n<p_Np){
        dfloat las = 0.f;

        // lift remaining surface terms
        #pragma unroll p_NfacesNfp
          for(int i=0;i<p_NfacesNfp;++i){
            las += LIFTT[n+i*p_Np]*s_lappflux[i];
          }

        s_lapp[n] -= las;
      }
    }

    @barrier("local");

    for(int n=0;n<p_maxNodes;++n;@inner(0)){
      if(n<p_Np){
        const dfloat J = vgeo[e*p_Nvgeo + p_JID];

        dfloat Mlaps = 0.f;

        // multiply by mass matrix
        #pragma unroll p_Np
        for(int i=0;i<p_Np;++i){
          Mlaps += MM[n+i*p_Np]*s_lapp[i];
        }

        const dlong id = e*p_Np+n;
        RHS[id] = J*Mlaps;
      }
    }
  }
}

// enter BCs if C0
@kernel void insPressureBCTet3D(const dlong Nelements,
                               @restrict const  dfloat *  sgeo,
                               @restrict const  dlong  *  vmapM,
                               @restrict const  int    *  mapB,
                               const dfloat time,
                               @restrict const  dfloat *  x,
                               @restrict const  dfloat *  y,
                               @restrict const  dfloat *  z,
                               const dfloat nu,
                               @restrict        dfloat *  P){

  for(dlong e=0;e<Nelements;e++;@outer(0)){
    for(int n=0;n<p_NfacesNfp;++n;@inner(0)){
      const dlong idM = vmapM[e*p_Nfp*p_Nfaces + n];
      const int bc = mapB[idM];

      if(bc>0) {
        const int face = n/p_Nfp;
        const dlong sid = p_Nsgeo*(e*p_Nfaces+face);
        const dfloat nx = sgeo[sid+p_NXID];
        const dfloat ny = sgeo[sid+p_NYID];
        const dfloat nz = sgeo[sid+p_NZID];

        const dfloat pM = P[idM];
        dfloat pP = 0.f;
        insPressureDirichletConditions3D(bc,nu,time, x[idM], y[idM], z[idM], nx, ny, nz, pM, &pP);
        P[idM] = pP;
      }
    }
  }
}